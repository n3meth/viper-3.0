import_code("./computer.src")
import_code("../../libs/error.src")
import_code("../../facades/computer-objects/shell.src")
import_code("../../utils/getUserFromHandler.src")

FtpShellSession = new ComputerSession
FtpShellSession.shell = null
FtpShellSession.type = function()
    return "ftpshell"
end function
FtpShellSession.getFileFrom = function(path, targetPath, otherSession)
    folder = self.getFile(path)
    if folder isa Error then return folder
    if folder.type != "FOLDER" then return Error.New("Path is not a folder")
    file = otherSession.getFile(targetPath)
    if file isa Error then return file
    if file.type != "FILE" then return Error.New("Target path is not a file")
    return otherSession.shell.transferFile(file.path(), folder.path(), self.shell)
end function
FtpShellSession.putFileTo = function(path, targetPath, otherSession)
    file = self.getFile(path)
    if file isa Error then return file
    if file.type != "FILE" then return Error.New("Path is not a file")
    folder = otherSession.getFile(targetPath)
    if folder isa Error then return folder
    if folder.type != "FOLDER" then return Error.New("Target path is not a folder")
    return self.shell.transferFile(file.path(), folder.path(), otherSession.shell)
end function
FtpShellSession.New = function(shell)
    result = new FtpShellSession
    result.shell = shell
    result.computer = shell.getComputer()
    result.root = climbFileToRoot(result.computer.getFile("/"))
    result.current = result.computer.getFile("/")
    result.user = getUserFromHandler(result)

    return result
end function